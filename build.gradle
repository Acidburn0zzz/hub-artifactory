buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'com.blackducksoftware.integration:common-gradle-plugin:0.0.+'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE'
    }
}

plugins {
    id 'groovy'
    id 'org.springframework.boot' version '1.5.8.RELEASE'
}

version = '5.0.0-SNAPSHOT'

apply plugin: 'com.blackducksoftware.integration.solution'

def versionFile = new File("${projectDir}/src/main/resources/version.txt")
versionFile.delete()
versionFile << version

task createFinalZip(type: Zip, dependsOn: build) {
    from("${projectDir}/src/main/resources/") {
        include 'version.txt'
        include 'blackDuckCacheInspector.properties'
        into 'plugins/lib'
    }

    from("${buildDir}/libs") { include '*.jar' }

    from("${projectDir}/src/main/groovy/com/blackducksoftware/integration/hub/artifactory/scan") {
        include 'blackDuckScanForHub.groovy'
        into 'plugins'
    }

    from("${projectDir}/src/main/groovy/com/blackducksoftware/integration/hub/artifactory/inspect") {
        include 'blackDuckCacheInspector.groovy'
        into 'plugins'
    }

    from(configurations.runtime) {
        exclude 'artifactory-papi*.jar'
        exclude 'commons-compress*.jar'
        exclude 'groovy-all*.jar'
        exclude 'gson*.jar'
        exclude 'logback*.jar'
        exclude 'snakeyaml*.jar'
        exclude 'spring*.jar'
        into 'plugins/lib'
    }
}

dependencies {
    compile 'com.blackducksoftware.integration:hub-common:26.0.0'
    compile 'com.blackducksoftware.integration:hub-artifactory-common:0.0.11'

    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework:spring-web'
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    compile 'org.apache.commons:commons-compress:1.13'

    compileOnly 'org.artifactory:artifactory-papi:4.14.0'
    compileOnly 'com.google.guava:guava:18.0'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

springBoot { mainClass = 'com.blackducksoftware.integration.hub.artifactory.Application' }
